!------------------------------------------------------------!
! This file is distributed as part of the cpw2000 code and   !
! under the terms of the GNU General Public License. See the !
! file `LICENSE' in the root directory of the cpw2000        !
! distribution, or http://www.gnu.org/copyleft/gpl.txt       !
!                                                            !
! The webpage of the cpw2000 code is not yet written         !
!                                                            !
! The cpw2000 code is hosted on GitHub:                      !
!                                                            !
! https://github.com/jlm785/cpw2000                          !
!------------------------------------------------------------!

!>     reads the metadata generated by rede from a file
!>     opened by a previous edsf_init

       subroutine read_esdf_rede(ipr,meta_cpw2000,lrede)

!      Written June 16, 2017. jlm
!      Modified, documentation, August 10 2019. JLM
!      copyright inesc-mn/Jose Luis Martins


!      version 4.94 of pw
!      version 1.51 of md

       use esdf

       implicit none


!      input

       integer, intent(in)                ::  ipr                        !<  print control: ipr < 3 no printing, except warnings.


!      output

       character(len=250), intent(out)    ::  meta_cpw2000               !<  metadata coded in cpw2000
       logical, intent(out)               ::  lrede                      !<  checks that NumberOfLatticePlanes was set

!      local variables

       integer            ::  nlines

       integer            ::  nlatpl                         ! number of lattice planes
       character(len=3)   ::  vers                           ! program version of rede
       character(len=50)  ::  title                          ! title of the calculation
       character(len=140) ::  name                           ! name of the calculation
       character(len=9)   ::  bdate                          ! date that rede.f90 was run
       character(len=8)   ::  btime                          ! time of day that rede.f90 was run
       integer            ::  isl(3,3)
       character(len=140) ::  subtitle                       ! subtitle of the calculation

       character(len=10)  ::  ymddate                        ! date in the year.month.day format

       integer            ::  kt, ktmax
       integer            ::  ioerr

!      counters

       integer   ::  i, j

       ktmax = 140

       nlatpl = 0

       vers = '0.0'

       do i = 1,50
         title(i:i) = ' '
       enddo

       do i = 1,140
         name(i:i) = ' '
       enddo

       bdate = '         '
       btime = '        '

       do i = 1,3
       do j = 1,3
         isl(i,j) = 0
       enddo
       enddo

       do i = 1,140
         subtitle(i:i) = ' '
       enddo

       do i = 1,250
         meta_cpw2000(i:i) = ' '
       enddo

!      Number of lattice planes

       nlatpl = esdf_integer('Rede.NumberOfLatticePlanes',nlatpl)

       lrede = .TRUE.
       if(nlatpl < 1) lrede = .FALSE.

       if(lrede) then

!        version of rede

         vers = esdf_string('Rede.Version',vers)


!        calculation title

         title = esdf_string('Rede.Title',title)


!        calculation name

         name = esdf_string('Rede.Name',name)


!        date rede.f90 was run (convert to old internal format)

         ymddate = esdf_string('Rede.Date',ymddate)
         call zedate_convert(bdate, ymddate, .TRUE.)

!        time of day rede.f90 was run

         btime = esdf_string('Rede.Time',btime)


!        Superlattice

         if(esdf_block('Rede.Superlattice',nlines)) then
           if(nlines /= 3) then
             write(6,*)
             write(6,*) '  esdf_read_rede:  Wrong Number of Lines'
             write(6,*) '  in Rede.Superlattice!'

             stop

           endif

           ioerr = 0
           do i=1,nlines

             read(block_data(i),*,iostat=ioerr) (isl(j,i),j=1,3)

             if(ioerr /= 0) then
               write(6,*)
               write(6,*) '    Stopped in read_esdf_rede'
               write(6,*) '    error reading Rede.Superlattice'

               stop

             endif

           enddo
         endif


!        subtitle

         subtitle(1:6) = '#NAME '
         kt = len(adjustl(trim(name)))
         kt = kt+6

         subtitle(7:kt) = adjustl(trim(name))

         if(kt < ktmax) then

           kt = kt+1
           write(subtitle(kt:kt),'(a1)') ' '

         endif

         if(kt+15 <= ktmax) then

           write(subtitle(kt+1:kt+15),'("#DATE ",a9)') bdate
           kt = kt+15

         endif


         if(kt+15 <= ktmax) then

           write(subtitle(kt+1:kt+15),'(" #TIME ",a8)') btime
           kt = kt+15

         endif


!        final encoding

         write(meta_cpw2000,'(3(3i4,2x),2x,"fcc SL",i3,"L v",a3,1x,      &
     &            a50,a140)')                                            &
     &            (isl(i,1),i=1,3),(isl(i,2),i=1,3),(isl(i,3),i=1,3),    &
     &            nlatpl,vers,title,subtitle

!        prints the results

         if(ipr > 2) then
           write(6,*)
           write(6,*)
           write(6,*)  '  The values set by read_esdf_rede are:'
           write(6,*)
           write(6,'("   The value of nlatpl is: ",i6)') nlatpl
           write(6,'("   The value of vers is: ",a3)') vers
           write(6,'("   The value of title is: ",a50)') title
           write(6,'("   The value of name is: ",a140)') name
           write(6,'("   The value of date is: ",a9)') bdate
           write(6,'("   The value of time is: ",a8)') btime
           write(6,'("   The value of isl is: ",3(3x,3i5))')             &
     &            ((isl(j,i),j=1,3),i=1,3)
           write(6,'("   The value of subtitle is: ",a140)') subtitle
           write(6,'("   The value of meta_cpw is: ",a250)')             &
     &            meta_cpw2000
           write(6,*)
           write(6,*)
         endif

       endif

       return
       end subroutine read_esdf_rede
