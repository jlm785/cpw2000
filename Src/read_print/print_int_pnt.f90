!------------------------------------------------------------!
! This file is distributed as part of the cpw2000 code and   !
! under the terms of the GNU General Public License. See the !
! file `LICENSE' in the root directory of the cpw2000        !
! distribution, or http://www.gnu.org/copyleft/gpl.txt       !
!                                                            !
! The webpage of the cpw2000 code is not yet written         !
!                                                            !
! The cpw2000 code is hosted on GitHub:                      !
!                                                            !
! https://github.com/jlm785/cpw2000                          !
!------------------------------------------------------------!

!>  prints the calculated k-points for integration
!>over the brillouin zone. periodic mesh.

subroutine print_int_pnt(ipr, nbandi, nx,ny,nz, sx,sy,sz,                &
  adot,                                                                  &
  nrk, rk, wgk, dkg,                                                     &
  mxdnrk)

! Extracted from int_pnt. 12 Novenber 2020. JLM
! copyright  J.L.Martins, INESC-MN.

! nx, ny, and nz are the number of points in the three
! directions dermined by the lattice wave vectors. sx, sy, and
! sz shifts the grid of integration points from the origin.

! version 4.99

  implicit none
  
  integer, parameter  :: REAL64 = selected_real_kind(12)

! input:

  integer, intent(in)             ::  mxdnrk                             !<  size of k-points

  integer, intent(in)             ::  nbandi                             !<  number of bands for every k-point
  integer, intent(in)             ::  nx,ny,nz                           !<  size of the integration mesh in k-space (nx*ny*nz)
  real(REAL64), intent(in)        ::  sx,sy,sz                           !<  offset of the integration mesh (0.5 for Monkhorst-Pack, 0.0 for DOS)
  integer, intent(in)             ::  ipr                                !<  controls printing (0,1,2). Higher value for more details
  real(REAL64), intent(in)        ::  adot(3,3)                          !<  metric in direct space

  integer, intent(in)             ::  nrk                                !<  number of k-points for integration in the irreducible wedge of the brillouin zone
  real(REAL64), intent(in)        ::  rk(3,mxdnrk)                       !<  component in lattice coordinates of the k-point in the mesh
  real(REAL64), intent(in)        ::  wgk(mxdnrk)                        !<  weight in the integration of k-point
  real(REAL64), intent(in)        ::  dkg(nx*ny*nz)                      !<  distance to nearest G-vector

! local arrays

  real(REAL64)  ::  rkcar(3)
  real(REAL64)  ::  bvec(3,3), avec(3,3)


! counters

  integer   ::  i, j, k
  

  call adot_to_avec_sym(adot,avec,bvec)

  if(ipr > 0) then
    write(6,*)
    write(6,'(1x,i5," k-points generated by program from",          &
      " parameters :")') nrk
    write(6,'(4x,"n =",3i5,6x,"s =",3f6.2,6x,"nb =",i5)')           &
               nx,ny,nz,sx,sy,sz,nbandi
    write(6,*)
  endif

  if(ipr > 1) then 
    write(6,*)
    write(6,'(7x,"i",7x,"w",17x,"rk",22x,"rkcar",12x,"ekin")')
    write(6,*)

    do i=1,nrk

!     rk in cartesian coordinates

      do k=1,3
        rkcar(k) = bvec(k,1)*rk(1,i) + bvec(k,2)*rk(2,i) +          &
                   bvec(k,3)*rk(3,i)
      enddo

!     printout

      write(6,'(1x,i7,3x,f8.4,3x,3f8.4,3x,3f7.3,3x,f7.3)')          &
         i,wgk(i),(rk(j,i),j=1,3),(rkcar(j),j=1,3),dkg(i)
    enddo
  endif

  return

end subroutine print_int_pnt
