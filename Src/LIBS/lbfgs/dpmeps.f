      REAL*8 FUNCTION DPMEPS()
C     **********
C
C     SUBROUTINE DPEPS
C
C     THIS SUBROUTINE COMPUTES THE MACHINE PRECISION PARAMETER
C     DPMEPS AS THE SMALLEST FLOATING POINT NUMBER SUCH THAT
C     1 + DPMEPS DIFFERS FROM 1.
C
C     THIS SUBROUTINE IS BASED ON THE SUBROUTINE MACHAR DESCRIBED IN
C
C     W. J. CODY,
C     MACHAR: A SUBROUTINE TO DYNAMICALLY DETERMINE MACHINE PARAMETERS,
C     ACM TRANS. MATH. SOFT., 14, 1988, PAGES 303-311.
C
C     THE SUBROUTINE STATEMENT IS:
C
C       SUBROUTINE DPEPS(DPMEPS)
C
C     WHERE
C
C       DPMEPS IS A DOUBLE PRECISION VARIABLE.
C         ON ENTRY DPMEPS NEED NOT BE SPECIFIED.
C         ON EXIT DPMEPS IS THE MACHINE PRECISION.
C
C     MINPACK-2 PROJECT. FEBRUARY 1991.
C     ARGONNE NATIONAL LABORATORY AND UNIVERSITY OF MINNESOTA.
C     BRETT M. AVERICK.
C
C     MODIFIED BY J.L.MARTINS TO AVOID INFINITE LOOPS WITH SMART COMPILERS
C
C     *******
      IMPLICIT NONE
C
      INTEGER I,IBETA,IRND,IT,ITEMP,NEGEP
      REAL*8 A,B,BETA,BETAIN,BETAH,TEMP,TEMPA,TEMP1,
     +       ZERO,ONE,TWO
      DATA ZERO,ONE,TWO /0.0D0,1.0D0,2.0D0/
CJLM
      INTEGER J
      REAL*8 SMALL
      DATA SMALL /1.0D-15/
CJLM
 
C     DETERMINE IBETA, BETA ALA MALCOLM.

      A = ONE
      B = ONE
      DO J=1,1000
         A = A + A
         TEMP = A + ONE
         TEMP1 = TEMP - A

         IF (TEMP1 - ONE .NE. ZERO) GO TO 10

      ENDDO
CJLM
C     INFINITE LOOP GIVE SOME REASONABLE VALUE

      DPMEPS = SMALL

      RETURN

CJLM

   10 CONTINUE

      DO J=1,1000
         B = B + B
         TEMP = A + B
         ITEMP = INT(TEMP - A)

         IF (ITEMP .NE. 0) GO TO 20

      ENDDO
CJLM
C     INFINITE LOOP GIVE SOME REASONABLE VALUE

      DPMEPS = SMALL

      RETURN

CJLM

   20 CONTINUE

      IBETA = ITEMP
      BETA = DBLE(IBETA)

C     DETERMINE IT, IRND.

      IT = 0
      B = ONE

      DO J=1,1000
         IT = IT + 1
         B = B * BETA
         TEMP = B + ONE
         TEMP1 = TEMP - B

         IF (TEMP1 - ONE .NE. ZERO) GO TO 30

      ENDDO
CJLM
C     INFINITE LOOP GIVE SOME REASONABLE VALUE

      DPMEPS = SMALL

      RETURN

CJLM

   30 CONTINUE

      IRND = 0
      BETAH = BETA/TWO
      TEMP = A + BETAH
      IF (TEMP - A .NE. ZERO) IRND = 1
      TEMPA = A + BETA
      TEMP = TEMPA + BETAH
      IF ((IRND .EQ. 0) .AND. (TEMP - TEMPA .NE. ZERO)) IRND = 2

C     DETERMINE DPMEPS.

      NEGEP = IT + 3
      BETAIN = ONE/BETA
      A = ONE
      DO I = 1, NEGEP
         A = A*BETAIN
      ENDDO

      DO J=1,1000

        TEMP = ONE + A

        IF (TEMP - ONE .NE. ZERO) GO TO 60
        A = A*BETA
      
      ENDDO
CJLM
C     INFINITE LOOP GIVE SOME REASONABLE VALUE

      DPMEPS = SMALL

      RETURN

CJLM

   60 CONTINUE

      DPMEPS = A
      IF ((IBETA .NE. 2) .AND. (IRND .NE. 0)) THEN
        A = (A*(ONE + A))/TWO
        TEMP = ONE + A
        IF (TEMP - ONE .NE. ZERO) DPMEPS = A
      ENDIF

      RETURN

      END

